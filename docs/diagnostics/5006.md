# 5006

Two types that were supposed to be compatible were not.

This is probably the most common typechecking error.

### Expected and found

Millet tries to report which type was "expected" and which was "found". For instance, in this example, we consider `int` the "expected" type, because of the annotation. This explicit annotation implies the programmer really thought it should be that type.

```sml
val x : int = "no"
(** + expected `int`, found `string` *)
```

This hints at a possible strategy for debugging this kind of error: if the expected and found types are confusing, try adding more type annotations.

### Function application

This error commonly occurs when applying a function to an argument, but the argument did not have the type the function expected. For instance, in this example, Millet reports that we "expected" `bool`, because the function `choose` takes a `bool`.

```sml
fun choose x = if x then "sosuke" else "pazu"
val _ = choose 4
(**            ^ expected `bool`, found `int` *)
```

### Fully qualified names

Millet reports types defined in structures and signatures prefixed with the names of the relevant structures or signatures. This is sometimes called a "fully qualified name" or "FQN".

```sml
signature FOO = sig
  type t
  val x : t
end

structure Foo :> FOO = struct
  type t = int
  val x = 3
end

val y = Foo.x : unit
(**     ^^^^^^^^^^^^ expected `unit`, found `Foo.t` *)
```

### Overloads

Certain built-in functions, like `+`, `<`, and `abs`, are overloaded, which means they may work with a certain fixed number of types. For instance, `+` works with `int`, `word`, and `real`, while `<` works for those as well as `string` and `char`.

```sml
val three : int = 1 + 2
val four : real = 1.5 + 2.5
```

When using overloaded functions, there must exist a single actual type being used. For instance, `+` works with `word`, `real`, and `int`. However, `+` cannot add a `real` to a `word`, or an `int` to a `real`, or any such similar combination. It can only add two `word`s, or two `real`s, or two `int`s.

```sml
val five = 2 + 3.0
(**        ^^^^^^^ `int` and `real` are incompatible *)
```

### Equality

Part of checking for type compatibility is ensuring that the equality attribute of types is respected. For instance, `=` and `<>` compare expressions whose type must be an equality type, and `real` is not an equality type, so the following fails.

```sml
val _ = 1.2 <> 3.4
(**     ^^^^^^^^^^ not an equality type *)
```

### Reporting types with invalid syntax

Millet uses pseudo-syntax that is not technically valid SML syntax to report some more exotic types.

It reports overloaded types as the following:

| Overload       | Meaning                                 |
| -------------- | --------------------------------------- |
| `<wordint>`    | `word`, `int`                           |
| `<realint>`    | `real`, `int`                           |
| `<num>`        | `word`, `real`, `int`                   |
| `<wordinttxt>` | `word`, `int`, `string`, `char`         |
| `<numtxt>`     | `word`, `real`, `int`, `string`, `char` |

Unsolved record types that may have other fields, which often arise with usages of `#` selectors, will be reported as record types with an extra `...` row.

If Millet encounters an invalid expression, like a variable that was undefined, it will report the type `_`. It will also report `_` for unsolved type variables.
