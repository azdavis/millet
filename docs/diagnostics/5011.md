# 5011

A pattern matcher, i.e. a `case` expression, `fn` literal, or sequence of `fun` cases, was not exhaustive.

Suppose we have a pattern matcher on an expression $e$ of type $\tau$.

If there exists a apparently constructable value $v$ of type $\tau$, such that there does _not_ exist a pattern in the pattern matcher that matches that value $v$, then this pattern matcher is _not_ exhaustive.

```sml
datatype d = A of string | B of int | C of bool

fun f (x : d) : int =
  case x of
(** + non-exhaustive case: missing `A _` *)
    B y => y
  | C z => if z then 1 else 2
```

In this example, `A "foo"` and `A "bar"` are examples of values not matched by the matcher.

In fact, `A s` for infinitely many strings `s` would not be matched. To communicate this, the error message notes that `A _` is not matched, where the `_` is understood to mean "any value of the appropriate type".

## A caveat about "apparently constructable"

Note that we said above (paraphrased):

> The exhaustiveness checker will error if there exists an unmatched **apparently constructable** value.

This is different from saying:

> The exhaustiveness checker will error if there exists an unmatched value.

The difference is illustrated by considering this example:

```sml
datatype void = Void of void
```

Here we define a type `void` which **has no values**. This is because to construct a `void` we would already need to have a `void` to pass to `Void`, the sole constructor for `void`. But the only `void`s are those constructed by `Void`. There is no "base case" for Void that allows us to actually write a value in the `...` hole below:

```sml
datatype void = Void of void

val myVoid : void = Void (Void (Void (...)))
(**                                   ^^^ hole with type `void` *)
```

So, it would seem that we should be able to, for example, match on values of type `void` with the "empty case", since there are no such values. Or, if we have a a value of type `void option`, we should be able to provide only a case for `NONE`, since there cannot exist any `SOME _` values, since the `_` would have to be a `void`.

However, the pattern matching exhaustiveness checker is not aware of such subtleties:

<!-- @ignore the warning about only one case arm -->

```sml
datatype void = Void of void

fun onlyNone (x : void option) : string =
(** + non-exhaustive case: missing `SOME _` *)
  case x of
    NONE => "okay"
```

This is what we mean by "apparently constructible": the exhaustiveness checker knows only that there is another constructor, `SOME`, for `'a option`, that wasn't matched. It does not use advanced knowledge about whether types can actually be constructible in a manner that would allow the above example to typecheck.

The best we can do is define a function `absurd` and use it to dispense of such cases:

```sml
datatype void = Void of void

fun absurd (Void v : void) : 'a = absurd v

fun onlyNone (x : void option) : string =
  case x of
    NONE => "okay"
  | SOME v => absurd v
```

## To fix

Add patterns matching the missing cases.

```sml
datatype d = A of string | B of int | C of bool

fun f (x : d) : int =
  case x of
    A s => String.size s
  | B y => y
  | C z => if z then 1 else 2
```
