# 5001

A name was referenced, but it was not defined in that scope.

```sml
val bar = foo
(**       ^^^ undefined value: `foo` *)
```

## To fix

Try any of the following:

### Check spelling and case

Names are case-sensitive in Standard ML and must match exactly with their definition site.

```sml
val foo = 3
val f = Foo
(** + undefined value: `Foo` *)
```

### Check if the name is within a structure

If the name is defined in the current scope unqualified, it may be referenced unqualified. But if it is within a structure, you must qualify the name with that structure.

For instance, `filter` is defined in `structure List`, not at the top level.

```sml
val f = filter
(** + undefined value: `filter` *)
```

Confusingly, some functions like `map` are defined both in `List` and also aliased the top level.

```sml
val m1 = List.map
val m2 = map (* an alias for `List.map` *)
```

You must either mention the structure in a qualified name or open the structure if the name is inside a structure.

```sml
val f1 = List.filter

local
  open List
in
  val f2 = filter
end
```

### Check the kind of name

Check the error message to see what kind of thing was not defined: value, type, structure, etc. These different kinds of items have different namespaces.

In this example, there is a value named `x` defined, but then we try to use `x` as a type. There is no type `x` defined, so this is invalid.

```sml
val x = 4
val y : x = 5
(**     ^ undefined type: `x` *)
```

### Check signature ascription

If the name is defined inside a structure, but the structure ascribes to a signature, and the signature does not include that name, then the name will not be available outside of the structure.

```sml
signature SIG = sig
  val inSigAndStruct : int
end

structure Str : SIG = struct
  val notInSig = "hi"
  val inSigAndStruct = String.size notInSig
end

(* this is fine *)
val n = Str.inSigAndStruct

(* this is not *)
val s = Str.notInSig
(**     ^^^^^^^^^^^^ undefined value: `notInSig` *)
```
