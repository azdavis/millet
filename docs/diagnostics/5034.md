# 5034

A functor application did not use the declaration "syntax sugar" when the functor definition did, or vice versa.

This example triggers the warning:

```sml
functor Func (
  val x : int
) = struct
  (* ... *)
end

structure S = Func (struct val x = 3 end)
(**           ^^^^^^^^^^^^^^^^^^^^^^^^^^^ the `functor` definition uses syntax sugar *)
```

This example triggers a warning, and also other errors:

```sml
signature HAS_T = sig
  type t
end

functor Func (
  structure Param : HAS_T
) = struct
  (* ... *)
end

structure Arg : HAS_T = struct
  type t = unit
end

structure S = Func (Arg)
(**           ^^^^^^^^^^ the `functor` definition uses syntax sugar *)
```

This is invalid, but the reason why is subtle. Before we explain the reason, note that the bottom line is that the call site of `Func` must be changed:

```diff
-structure S = Func (Arg)
+structure S = Func (structure Param = Arg)
```

The fact that both the definition site and the corrected call site for `Func` have an extra `structure` keyword is a clue.

The key is in the definition of `Func`. We use the syntax:

<!-- @ignore invalid syntax, fragment of a functor dec -->

```sml
functor Func (
  structure Param : HAS_T
)
```

which has a distinct meaning from:

<!-- @ignore invalid syntax, fragment of a functor dec -->

```sml
functor Func (
  Param : HAS_T
)
```

Both forms are valid SML. The first form is syntax sugar. This means it is extra "helper" syntax that is fully defined in terms of the second, more "fundamental" syntax.

In the example, the sugar is expanded approximately in the following manner:

<!-- @ignore builds off of previous example, HAS_T is not defined -->

```sml
(* original *)
functor Func (
  structure Param : HAS_T
) = struct
  (* ... *)
end

(* desugared *)
functor Func (Anonymous : sig
  structure Param : HAS_T
end) = let
  open Anonymous
in
  struct
    (* ... *)
  end
end
```

Note that in this illustration, we've named the desugared functor argument `Anonymous`. This is to emphasize that when expanding the syntax sugar, an SML implementation will generate an anonymous, unutterable[,][accursed] unique structure name for the argument.

Note also that functor body uses `let` to open this anonymous structure, allowing the `struct (* ... *) end` region to use the names that were in the anonymous structure (like `Param`, in this case).

Similarly, once we modify the call site, we are using more syntax sugar, which also expands:

<!-- @ignore build off of previous example, Func is not defined -->

```sml
(* original *)
structure S = Func (structure Param = Arg)

(* desugared *)
structure S = Func (struct structure Param = Arg end)
```

Thus, a similar but "opposite" error may occur if the definition site does not use the syntax sugar, but the call site does. As in:

```sml
signature HAS_T = sig
  type t
end

functor Func (
  Param : HAS_T
) = struct
  (* ... *)
end

structure Arg : HAS_T = struct
  type t = unit
end

structure S = Func (structure Param = Arg)
(**           ^^^^^^^^^^^^^^^^^^^^^^^^^^^^ the `functor` application uses syntax sugar *)
```

This will error. To fix, do not use the syntax sugar at the call site.

```diff
-structure S = Func (structure Param = Arg)
+structure S = Func (Arg)
```

[accursed]: https://free.cofree.io/2020/07/20/perform-io/
