# 5033

The equality function `=` or the inequality function `<>` was applied to a discouraged argument.

```sml
fun isEmpty xs = xs = []
(**              ^^^^^^^ calling `=` or `<>` on `nil` *)
```

Using `=` or `<>` may unnecessarily restrict the type to be an "equality" type. Millet will report the type of `isEmpty` above as `''a list -> bool` instead of the perhaps expected `'a list -> bool`.

Note the extra tick mark: `''a` is a type variable that can only be substituted with equality types, whereas `'a` is an unrestricted type variable. This is due to the usage of `=`.

Although `bool` is already an equality type, it is also discouraged to call `=` or `<>` on boolean literals, because the expression can be simplified:

| Complex      | Simple  |
| ------------ | ------- |
| `x = true`   | `x`     |
| `x = false`  | `not x` |
| `x <> true`  | `not x` |
| `x <> false` | `x`     |

## To fix

If the error is on a `bool` literal, consult the above table to simplify the expression.

For lists and options, consider whether you need to access the values inside the constructors, e.g. the head or tail of the list, or the value in a `SOME`.

### If you need the values inside the constructors

Use pattern matching.

Before, with lists:

```sml
fun sum xs =
  if xs = [] then
(**  ^^^^^^^ calling `=` or `<>` on `nil` *)
    0
  else
    hd xs + sum (tl xs)
```

After, with lists:

```sml
fun sum xs =
  case xs of
    [] => 0
  | x :: r => x + sum r
```

Before, with options:

```sml
fun foo opt =
  if opt = NONE then
(**  ^^^^^^^^^^ calling `=` or `<>` on `NONE` *)
    "hi"
  else
    valOf opt ^ "!"
```

After, with options:

```sml
fun foo opt =
  case opt of
    NONE => "hi"
  | SOME x => x ^ "!"
```

### If you don't need the values inside the constructors

Use the convenience functions `List.null` and `Option.isSome`.

Before, with lists:

```sml
fun reportList xs =
  if xs = [] then
(**  ^^^^^^^ calling `=` or `<>` on `nil` *)
    "empty list"
  else
    "non empty list"
```

After, with lists:

```sml
fun reportList xs =
  if List.null xs then
    "empty list"
  else
    "non empty list"
```

Before, with options:

```sml
fun reportOption opt =
  if opt = NONE then
(**  ^^^^^^^^^^ calling `=` or `<>` on `NONE` *)
    "nothing"
  else
    "something inside"
```

After, with options:

```sml

fun reportOption opt =
  if Option.isSome opt then
    "something inside"
  else
    "nothing"
```

Note that the `if` and `else` case got switched.
