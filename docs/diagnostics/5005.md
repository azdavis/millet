# 5005

Typechecking failed, because of a "circular type".

## TL;DR

It's likely that when defining a recursive function, there is a recursive call that has too many or too few arguments passed to it.

## Explanation

This error occurs when SML type inference attempted to take a type variable $\alpha$, and set it equal to a type $\tau$, where that type $\tau$ contains the original type variable $\alpha$ itself.

### Too few arguments

Consider this example:

```sml
fun last xs =
(** + circular type: `?a` occurs in `?a list -> ?a` *)
  case xs of
    [] => raise Empty
  | [x] => x
  | _ :: xs' => last
(**      ^^^ unused value *)
```

We are attempting to implement our own version of `List.last`, which returns the last element of the list or raises `Empty` if given an empty list.

But we made a mistake in the last line of the function definition: we forgot to pass the argument `xs'` to `last` in the recursive call. (Note the warning.) The fix is to pass the argument:

```diff
 fun last xs =
   case xs of
     [] => raise Empty
   | [x] => x
-  | _ :: xs' => last
+  | _ :: xs' => last xs'
```

But as it stands right now, we get this error about a "circular type". Why is that?

First, note that `last` has no type annotations on either its parameters or return type. This is allowed in SML. But it means we must use type inference to determine the type of `last`.

We know `last` is a function. So it'll have a function type, like `_ -> _`. Let's say:

- `?xs` is the type of `xs`, the parameter.
- `?ret` is the return type.

So `last` has type `?xs -> ?ret`.

We see we case on `xs` like it's a list. So we know `?xs = ?a list` where `?a` is the type of the elements.

The first case where we `raise Empty` doesn't tell us any new information about the return type, since a `raise` may have any type.

The next case, though, does tell us new information. We bind `x`, the single element of `xs`, and return it. `xs` has type `?a list`, so `x` has type `?a`, the type of elements in `xs`. And we return `x`, so `?ret = ?a`.

We said `last` has type `?xs -> ?ret`. Substituting in our solutions for `?xs` and `?ret`, we now have that `last` has type `?a list -> ?a`.

And now we see the problem in the last case. We return `last` itself, so `?ret = ?a list -> ?a`. But `?ret = ?a`, so we have `?a = ?a list -> ?a`.

This is the "circular type": we attempt to solve the type variable `?a` to the type `?a list -> ?a`, but that type contains `?a` itself.

### Too many arguments

Suppose we had this instead:

```sml
fun last xs =
(** + circular type: `?a` occurs in `unit -> ?a` *)
  case xs of
    [] => raise Empty
  | [x] => x
  | _ :: xs' => last xs' ()
```

Again, we made a small mistake in implementing `last`: we passed an extra spurious `()` in the recursive call. And again, the error mentions a circular type, for similar reasons as described above.

### Applying something to itself

This error occurs in situations like this as well:

```sml
fun f x = x x
(**       ^ circular type: `?a` occurs in `?a -> ?b` *)
```

## To fix

- As mentioned above, check if there are too many or too few arguments being passed to a recursive function.
- As with [other tricky type errors](./5006.md), consider adding more type annotations so type inference is less likely to get "confused".
